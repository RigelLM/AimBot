cmake_minimum_required(VERSION 3.20)
project(aimbot LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

# ---------------- OpenCV ----------------
set(OpenCV_DIR "${CMAKE_SOURCE_DIR}/vendor/opencv/build")
find_package(OpenCV REQUIRED CONFIG)

# ---------------- Core library: capture + vision + viz + cursor + targetlock----------------
add_library(aimbot_core STATIC
  "${CMAKE_SOURCE_DIR}/src/capture/DxgiDesktopDuplicationSource.cpp"
  "${CMAKE_SOURCE_DIR}/src/vision/HsvMasker.cpp"
  "${CMAKE_SOURCE_DIR}/src/vision/ContourDetector.cpp"
  "${CMAKE_SOURCE_DIR}/src/viz/OverlayRenderer.cpp"
  "${CMAKE_SOURCE_DIR}/src/cursor/CursorAssist.cpp"
  "${CMAKE_SOURCE_DIR}/src/lock/TargetLock.cpp"
  "${CMAKE_SOURCE_DIR}/src/lock/LockMatcher.cpp"
  "${CMAKE_SOURCE_DIR}/src/lock/NearestToAimSelector.cpp"
)

target_include_directories(aimbot_core PUBLIC
  "${CMAKE_SOURCE_DIR}/include"
  "${CMAKE_SOURCE_DIR}/vendor/"
  ${OpenCV_INCLUDE_DIRS}
)

target_link_libraries(aimbot_core PUBLIC
  ${OpenCV_LIBS}
)

if (WIN32)
  target_link_libraries(aimbot_core PUBLIC d3d11 dxgi)
endif()

# ---------------- ImGui (Win32 + DX11) ----------------
add_library(imgui STATIC
  "${CMAKE_SOURCE_DIR}/vendor/imgui/imgui.cpp"
  "${CMAKE_SOURCE_DIR}/vendor/imgui/imgui_draw.cpp"
  "${CMAKE_SOURCE_DIR}/vendor/imgui/imgui_tables.cpp"
  "${CMAKE_SOURCE_DIR}/vendor/imgui/imgui_widgets.cpp"
  "${CMAKE_SOURCE_DIR}/vendor/imgui/imgui_demo.cpp"
  "${CMAKE_SOURCE_DIR}/vendor/imgui/backends/imgui_impl_win32.cpp"
  "${CMAKE_SOURCE_DIR}/vendor/imgui/backends/imgui_impl_dx11.cpp"
)

target_include_directories(imgui PUBLIC
  "${CMAKE_SOURCE_DIR}/vendor/imgui"
  "${CMAKE_SOURCE_DIR}/vendor/imgui/backends"
)

# ImGui DX11/Win32
if (WIN32)
  target_link_libraries(imgui PUBLIC d3d11 dxgi imm32)
endif()

# ---------------- GUI library: ImGui app shell ----------------
add_library(aimbot_gui STATIC
  "${CMAKE_SOURCE_DIR}/src/gui/ImGuiApp.cpp"
)

target_include_directories(aimbot_gui PUBLIC
  "${CMAKE_SOURCE_DIR}/include"
)

target_link_libraries(aimbot_gui PUBLIC
  imgui
  aimbot_core
)

# ---------------- App executable: main only ----------------
add_executable(${PROJECT_NAME}
  "${CMAKE_SOURCE_DIR}/src/app/main.cpp"
)

target_link_libraries(${PROJECT_NAME} PRIVATE aimbot_gui)

# ---------------- Copy OpenCV DLLs ----------------
function(copy_opencv_dlls_to_target target_name)
  if(WIN32)
    set(OPENCV_BIN_DIR "${CMAKE_SOURCE_DIR}/vendor/opencv/build/x64/vc16/bin")
    add_custom_command(TARGET ${target_name} POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_directory
        "${OPENCV_BIN_DIR}"
        "$<TARGET_FILE_DIR:${target_name}>"
    )
  endif()
endfunction()

# ---------------- Copy Config files ----------------
function(copy_config_files_to_target target_name)
    add_custom_command(TARGET ${target_name} POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_directory
        "${CMAKE_SOURCE_DIR}/config"
        "$<TARGET_FILE_DIR:${target_name}>/config"
    )
endfunction()

copy_opencv_dlls_to_target(${PROJECT_NAME})
copy_config_files_to_target(${PROJECT_NAME})

option(BUILD_EXAMPLES "Build quick experimental executables" ON)

if(BUILD_EXAMPLES)
    add_subdirectory(examples)
endif()